Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_TumbleThroughSpells("Target_TumbleThrough");
KBSECTION
//REGION Make enemies passable
// When the character that applied Tumble Through to an NPC is
// the actively controlled character, we need that NPC to gain
// a status that makes them CanWalkThrough.
IF
StatusApplied((GUIDSTRING)_NPC, "TUMBLE_THROUGH_TARGET", (GUIDSTRING)_Character, _)
AND
HasActiveStatus(_Character, "TUMBLE_THROUGH_SOURCE", 1)
THEN
DB_ActiveTumbleThrough(_Character, _NPC);
TimerLaunch("TumbleThroughUpdate", 100);

IF
TimerFinished("TumbleThroughUpdate")
AND
DB_ActiveTumbleThrough((GUIDSTRING)_Character, (GUIDSTRING)_NPC)
AND
GetReservedUserID((CHARACTER)_Character, (INTEGER)_User)
AND
GetCurrentCharacter(_User, _CurrentCharacter)
AND
_Character == _CurrentCharacter
AND
HasActiveStatus(_NPC, "TUMBLE_THROUGH_WALKABLE", 0)
THEN
ApplyStatus(_NPC, "TUMBLE_THROUGH_WALKABLE", -1.0, 1, (GUIDSTRING)_Character);

IF
TimerFinished("TumbleThroughUpdate")
AND
DB_ActiveTumbleThrough((GUIDSTRING)_Character, (GUIDSTRING)_NPC)
AND
GetReservedUserID((CHARACTER)_Character, (INTEGER)_User)
AND
GetCurrentCharacter(_User, _CurrentCharacter)
AND
_Character != _CurrentCharacter
AND
HasActiveStatus(_NPC, "TUMBLE_THROUGH_WALKABLE", 1)
THEN
RemoveStatus(_NPC, "TUMBLE_THROUGH_WALKABLE", (GUIDSTRING)_Character);

IF
TimerFinished("TumbleThroughUpdate")
AND
DB_ActiveTumbleThrough((GUIDSTRING)_Character, (GUIDSTRING)_NPC)
AND
HasActiveStatus(_NPC, "TUMBLE_THROUGH_TARGET", 1)
THEN
TimerLaunch("TumbleThroughUpdate", 100);

IF
TimerFinished("TumbleThroughUpdate")
AND
DB_ActiveTumbleThrough((GUIDSTRING)_Character, (GUIDSTRING)_NPC)
AND
HasActiveStatus(_NPC, "TUMBLE_THROUGH_TARGET", 0)
THEN
RemoveStatus(_NPC, "TUMBLE_THROUGH_WALKABLE", (GUIDSTRING)_Character);
NOT DB_ActiveTumbleThrough(_Character, _NPC);
//END_REGION

//REGION Reactive Strike
// Failing a TT check applies a status to let us trigger
// AoO when casting TT is done.
IF
CastedSpell((GUIDSTRING)_Character, (STRING)_Spell, _, _, _)
AND
DB_TumbleThroughSpells(_Spell)
THEN
DB_TumbleThroughCastFinished(_Character);
TimerLaunch("TumbleThroughCastFinished", 60);

IF
TimerFinished("TumbleThroughCastFinished")
AND
DB_TumbleThroughCastFinished((GUIDSTRING)_Character)
AND
HasActiveStatus(_Character, "TUMBLE_THROUGH_FAILED", 1)
THEN
NOT DB_TumbleThroughCastFinished((GUIDSTRING)_Character);
UseSpell(_Character ,"Shout_ReactToMove", _Character , _Character, 0);
RemoveStatus(_Character, "TUMBLE_THROUGH_FAILED");
//END_REGION

//REGION Short Travel
// When the TUMBLE_THROUGH_SHORT status is applied, it indicates
// that the character ran out of movement before completing their
// tumble through and short be shunted back to their original
// position. Clear out previous casts from DB first.
IF
UsingSpellOnTarget((GUIDSTRING)_Character, _, (STRING)_Spell, _, _, _)
AND
DB_TumbleThroughSpells(_Spell)
AND
DB_TumbleThroughCastPositions(_Character, (GUIDSTRING)_OldTarget, (REAL)_OldX, (REAL)_OldY, (REAL)_OldZ)
THEN
NOT DB_TumbleThroughCastPositions(_Character, _OldTarget, _OldX, _OldY, _OldZ);

IF
UsingSpellOnTarget((GUIDSTRING)_Character, (GUIDSTRING)_Target, (STRING)_Spell, _, _, _)
AND
DB_TumbleThroughSpells(_Spell)
AND
GetPosition(_Character, (REAL)_X, (REAL)_Y, (REAL)_Z)
THEN
DB_TumbleThroughCastPositions(_Character, _Target, _X, _Y, _Z);

IF
StatusApplied((GUIDSTRING)_Character, "TUMBLE_THROUGH_SHORT", _, _)
AND
DB_TumbleThroughCastPositions(_Character, (GUIDSTRING)_Target, (REAL)_X, (REAL)_Y, (REAL)_Z)
THEN
CreateExplosionAtPosition(_X, _Y, _Z, "Projectile_TumbleThroughKnockback", 1, _Target);
NOT DB_TumbleThroughCastPositions(_Character, _Target, _X, _Y, _Z);
DB_TumbleThroughKnockbackTarget(_Character);
TimerLaunch("TumbleThroughKnockback", 60);

IF
TimerFinished("TumbleThroughKnockback")
AND
DB_TumbleThroughKnockbackTarget((GUIDSTRING)_Character)
THEN
RemoveStatus(_Character, "TUMBLE_THROUGH_SHORT");
NOT DB_TumbleThroughKnockbackTarget(_Character);
//END_REGION
EXITSECTION

ENDEXITSECTION
